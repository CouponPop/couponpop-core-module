plugins {
    id 'java-library'
    id 'maven-publish'
}

ext {
    baseSnapshotVersion = '0.0.15'
    lombokVersion = '1.18.40'
}
group = 'com.couponpop'
version = project.findProperty('version') ?: '0.0.0'
description = 'couponpop-core-module'

java {
    sourceCompatibility = '17'
    targetCompatibility = '17'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // [Jackson]
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'

    // [Compile & Runtime & Test]
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = group
            artifactId = "couponpop-core"
            version = version
            from components.java
            pom {
                name.set("CouponPop Core Module")
                description.set("Common core utilities for CouponPop microservices")
                url.set("https://github.com/CouponPop/couponpop-core-module")
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/CouponPop/couponpop-core-module"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes(
                'Manifest-Version': '1.0',
                'Implementation-Title': 'CouponPop Core Module',
                'Implementation-Version': version,
                'Implementation-Vendor': 'CouponPop Team',
                'Automatic-Module-Name': 'com.couponpop.core'
        )
    }
}

tasks.withType(PublishToMavenRepository).configureEach {
    if (System.getenv("GITHUB_ACTIONS") == "true") {
        repository.credentials {
            username = System.getenv("GITHUB_ACTOR")
            password = System.getenv("GITHUB_TOKEN")
        }
    }
}

tasks.register('printBaseSnapshotVersion') {
    doLast {
        println project.ext.baseSnapshotVersion
    }
}